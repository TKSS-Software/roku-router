import "pkg:/source/router.bs"
import "pkg:/source/roku_modules/rodash/rodash.brs"
import "pkg:/source/roku_modules/promises/promises.brs"

sub init()
    m._allowHandleFocus = false
end sub

function _beforeViewOpen(params = {} as Object) as Dynamic
    return promises.chain(beforeViewOpen(params), params).then(function(response as Dynamic, params as Dynamic) as Dynamic
        if params.route.routeConfig.isDialog AND NOT m.router.appLaunchComplete then
            m.top.signalBeacon("AppDialogInitiate")
        end if
        return response
    end function).toPromise()
end function

function beforeViewOpen(params = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _onViewOpen(params = {} as Object) as Dynamic
    return promises.chain(onViewOpen(params), params).then(function(response as Dynamic, params as Dynamic) as Dynamic
        if NOT params.route.routeConfig.isDialog AND NOT m.top.router.appLaunchComplete then
            m.top.signalBeacon("AppLaunchComplete")
            m.top.router.appLaunchComplete = true
        end if

        m._allowHandleFocus = true
        return response
    end function).toPromise()
end function

function onViewOpen(params = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _beforeViewClose(params = {} as Object) as Dynamic
    m._allowHandleFocus = false
    if params.route.routeConfig.isDialog AND NOT m.top.router.appLaunchComplete then
        m.top.signalBeacon("AppDialogComplete")
    end if
    return beforeViewClose(params)
end function

function beforeViewClose(params = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _onRouteUpdate(event as RouteUpdateEvent) as Dynamic
    print "Route is updating", event.oldRoute, event.newRoute 'bs:disable-line 1001 LINT3012
    return onRouteUpdate(event)
end function

function onRouteUpdate(event = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _onViewResume(_ as Dynamic, applyFocus = false as Boolean) as Dynamic
    return onViewResume(_)
end function

function _onViewSuspend(_ as Dynamic) as Dynamic
    return onViewSuspend(_)
end function

function onViewResume(_ as Dynamic) as Dynamic
    return promises.resolve(Invalid)
end function

function onViewSuspend(_ as Dynamic) as Dynamic
    return promises.resolve(Invalid)
end function

function _handleFocus(data = {} as Object) as Boolean
    if m._allowHandleFocus then
        handled = handleFocus(data)
        if handled then
            ' print "RouterView focused", data
        else
            ' print "RouterView not focused", data
        end if
        return handled
    end if
    return false
end function

function handleFocus(data = {} as Object) as Boolean
    return false
end function
