import "../../../source/common.bs"

sub init()
    m.rowList = m.top.findNode("rowList")
    m.rowList.observeField("rowItemSelected", "onItemSelected")
end sub

sub onItemSelected(event as Object)
    itemSelected = event.getRoSGNode().content.getChild(event.getData()[0]).getChild(event.getData()[1])
    path = `/details/${m.pageType}/${itemSelected.id}/about`
    rokuRouter.navigateTo(path)
end sub

function beforeViewOpen(params = Invalid as Dynamic) as Dynamic
    m.pageType = params.route.routeParams.type

    content = util.getContentById(params.route.routeParams.id)

    api = { data: { carousels: [] } }
    api.data.carousels.push(util.getRelatedCarousel(content, m.pageType))

    rowListContent = rodash.createNode("ContentNode")
    if rodash.isNonEmptyAA(api) then
        for each carousel in api["data"]["carousels"]
            if rodash.isNonEmptyArray(carousel["items"]) then
                row = rodash.createNode("ContentNode", {
                    "title": carousel["title"],
                })
                for each item in carousel["items"]
                    content = rodash.createNode("ContentNode", item)
                    row.appendChild(content)
                end for
                rowListContent.appendChild(row)
            end if
        end for
    end if
    m.rowList.itemComponentName = "CatalogScreenItem"
    m.rowList.itemSize = [1800, 440]
    m.rowList.rowItemSize = [[220, 330]]
    m.rowList.rowLabelOffset = [0, 20]
    m.rowList.showRowLabel = [true]
    m.rowList.numRows = 3
    m.rowList.rowItemSpacing = [20, 20]
    m.rowList.content = rowListContent
    return promises.resolve(true)
end function


function handleFocus(data as Object) as Boolean
    if data.routerHasFocus AND data.outletinfocuschain then
        return m.rowList.setFocus(true)
    end if

    return false
end function
