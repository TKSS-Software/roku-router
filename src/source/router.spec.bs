import "pkg:/source/router.bs"

function onRouterStateChanged(event as Object)
    data = event.getData()
    globalAA = getGlobalAA()
    globalAA.routerStatesHistory.push(data.type)
end function

namespace tests

    @async
    @SGNode("Group")
    @suite("rokuRouter namespace")
    class rokuRouter extends rooibos.BaseTestSuite
        protected override function beforeEach()
            router = rokuRouter.getRouter()
            if router <> Invalid
                router.unobserveField("routerState")
            end if

            children = m.top.getChildren(-1, 0)
            m.top.removeChildren(children)

            m.outlet = createObject("roSGNode", "Outlet")
            m.top.appendChild(m.outlet)


            globalAA = getGlobalAA()
            globalAA.routerStatesHistory = []

            rokuRouter.initialize({
                router: createObject("roSGNode", "Router"),
                outlet: m.outlet
            })

        end function

        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @describe("addRoutes")
        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @it("tests adding and getting routes")
        function _()
            'register some routes
            rokuRouter.addRoutes([
                { pattern: "/alpha", component: "View" },
                { pattern: "/beta", component: "View" },
            ])

            m.assertEqual(rokuRouter.getRoutes(), {
                "/alpha": { pattern: "/alpha", component: "View" },
                "/beta": { pattern: "/beta", component: "View" }
            })
        end function


        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @describe("navigateTo")
        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @async()
        @it("navigates to a known path")
        function _()
            globalAA = getGlobalAA()
            globalAA.routerStatesHistory = []

            rokuRouter.getRouter().unobserveField("routerState")
            rokuRouter.getRouter().observeField("routerState", "onRouterStateChanged")

            'register some routes
            rokuRouter.addRoutes([
                { pattern: "/alpha", component: "View" },
                { pattern: "/beta", component: "View" },
            ])

            navigatePromise = rokuRouter.navigateTo("/alpha")

            'navigate to the first route
            return promises.chain(navigatePromise, m).then(function(_result, m)
                m.assertEqual(rokuRouter.getRouter().routerState.url, "/alpha")
            end function).catch(function(_error = Invalid)
                print "error", _error
            end function).finally(function(m = Invalid)
                m.assertEqual(getGlobalAA().routerStatesHistory, [
                    "NavigationStart"
                    "RoutesRecognized"
                    "GuardsCheckStart"
                    "ActivationStart"
                    "GuardsCheckEnd"
                    "ResolveStart"
                    "ActivationEnd"
                    "ResolveEnd"
                    "NavigationEnd"
                ])
            end function).toPromise()
        end function

        @async()
        @it("navigates to an unknown path")
        function _()
            globalAA = getGlobalAA()
            globalAA.routerStatesHistory = []

            rokuRouter.getRouter().unobserveField("routerState")
            rokuRouter.getRouter().observeField("routerState", "onRouterStateChanged")

            'register some routes
            rokuRouter.addRoutes([
                { pattern: "/alpha", component: "View" },
                { pattern: "/beta", component: "View" },
            ])

            navigatePromise = rokuRouter.navigateTo("/charlie")

            'navigate to the first route
            return promises.chain(navigatePromise, m).finally(function(m = Invalid)
                m.assertEqual(getGlobalAA().routerStatesHistory, [
                    "NavigationStart"
                    "NavigationError"
                ])
            end function).toPromise()
        end function

        @async()
        @it("Route has no View component")
        function _()
            rokuRouter.getRouter().observeField("routerState", "onRouterStateChanged")

            'register some routes
            rokuRouter.addRoutes([
                { pattern: "/alpha" }
            ])

            navigatePromise = rokuRouter.navigateTo("/alpha")

            'navigate to the first route
            return promises.chain(navigatePromise, m).finally(function(m = Invalid)
                m.assertEqual(getGlobalAA().routerStatesHistory, [
                    "NavigationStart"
                    "NavigationError"
                ])
            end function).toPromise()
        end function

        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @describe("createRedirectCommand")
        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @it("creates a command")
        function _()
            command = rokuRouter.createRedirectCommand("alpha/beta")
            m.assertEqual(command, {
                command: "RedirectCommand",
                path: "alpha/beta",
                context: {},
                routeConfigOverrides: {}
            })
        end function

        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @describe("getRoutes")
        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @it("getsRoutes")
        function _()
            rokuRouter.addRoutes([
                { pattern: "/alpha", component: "View" },
                { pattern: "/beta", component: "View" },
            ])

            m.assertEqual(rokuRouter.getRoutes(), {
                "/alpha": {
                    "component": "View"
                    "pattern": "/alpha"
                },
                "/beta": {
                    "component": "View"
                    "pattern": "/beta"
                }
            })
        end function
    end class
end namespace
