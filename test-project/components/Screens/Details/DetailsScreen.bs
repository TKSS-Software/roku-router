import "../../../source/roku_modules/rodash/rodash.brs"
import "../../../source/roku_modules/promises/promises.brs"
import "../../../source/common.bs"

sub init()
    m.rowList.observeField("rowItemSelected", "onItemSelected")
end sub

sub onItemSelected(event as Object)
    itemSelected = event.getRoSGNode().content.getChild(event.getData()[0]).getChild(event.getData()[1])
    path = `/details/${m.pageType}/${itemSelected.id}`
    Router.navigateTo(path)
end sub

function beforeViewOpen(params = Invalid as Dynamic) as Dynamic
    m.pageType = params.routeParams.type

    content = util.getContentById(params.routeParams.id)

    print "content", content

    textWidths = 1920 - (m.content.translation[0] * 2) - m.topSection.itemSpacings[0] - m.poster.width

    m.title.font = "font:MediumBoldSystemFont"
    m.title.width = textWidths
    m.title.text = rodash.getString(content, "name")

    m.description.text = rodash.getString(content, "overview")
    m.description.width = textWidths

    m.rating.font = "font:SmallestSystemFont"
    m.rating.text = `Rating: ${rodash.get(content, "vote_average")}`

    m.poster.uri = rodash.getString(content, "poster_path").replace("/original/", `/w${m.poster.width}_and_h${m.poster.height}_multi_faces/`)
    m.background.uri = rodash.getString(content, "backdrop_path").replace("/original/", `/w1920_and_h1080_multi_faces/`)

    api = { data: { carousels: [] } }
    api.data.carousels.push(util.getRelatedCarousel(content, m.pageType))

    rowListContent = rodash.createNode("ContentNode")
    if rodash.isNonEmptyAA(api) then
        for each carousel in api["data"]["carousels"]
            if rodash.isNonEmptyArray(carousel["items"]) then
                row = rodash.createNode("ContentNode", {
                    "title": carousel["title"],
                })
                for each item in carousel["items"]
                    content = rodash.createNode("ContentNode", item)
                    row.appendChild(content)
                end for
                rowListContent.appendChild(row)
            end if
        end for
    end if
    m.rowList.itemComponentName = "CatalogScreenItem"
    m.rowList.itemSize = [1800, 440]
    m.rowList.rowItemSize = [[220, 330]]
    m.rowList.rowLabelOffset = [0, 20]
    m.rowList.showRowLabel = [true]
    m.rowList.numRows = 3
    m.rowList.rowItemSpacing = [20, 20]
    m.rowList.content = rowListContent
    return promises.resolve(Invalid)
end function

function onViewOpen(params = Invalid as Dynamic) as Dynamic
    m.rowList.setFocus(true)
    return promises.resolve(Invalid)
end function

function onViewResume(content = Invalid as Dynamic) as Dynamic
    m.rowList.setFocus(true)
    return promises.resolve(Invalid)
end function
