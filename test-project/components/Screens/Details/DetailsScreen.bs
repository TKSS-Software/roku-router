import "../../../source/roku_modules/rodash/rodash.brs"
import "../../../source/roku_modules/promises/promises.brs"
import "../../../source/common.bs"

sub init()
    m.content = Invalid
    m.childOutlet = m.top.findNode("childOutlet")
    m.container = m.top.findNode("container")
    m.textWidths = 0
    m.title = m.top.findNode("title")
    m.poster = m.top.findNode("poster")
    m.background = m.top.findNode("background")
    m.buttonGroup = m.top.findNode("buttonGroup")
    m.buttonGroup.buttons = ["About", "Related"]
    m.buttonGroup.observeField("buttonSelected", "onButtonPress")

    m.aboutUrl = ""
    m.relatedUrl = ""
    m.lastFocused = m.buttonGroup
end sub

sub onButtonPress(event as Object)
    button = event.getData()
    navigateTo = Invalid

    if button = 0 then
        navigateTo = m.aboutUrl
    else if button = 1 then
        navigateTo = m.relatedUrl
    end if

    if navigateTo <> Invalid then
        promises.chain(rokuRouter.navigateTo(navigateTo)).then(function(response as Dynamic)
            m.buttonGroup.setFocus(true)
        end function).toPromise()
    end if
end sub

function beforeViewOpen(params = Invalid as Dynamic) as Dynamic
    m.pageType = params.route.routeParams.type
    m.pageId = params.route.routeParams.id
    m.content = util.getContentById(params.route.routeParams.id)

    m.textWidths = 1920 - (m.container.translation[0] * 2) - m.container.itemSpacings[0] - m.poster.width
    m.top.findNode("divider").width = m.textWidths

    m.title.font = "font:MediumBoldSystemFont"
    m.title.width = m.textWidths
    date = CreateObject("roDateTime")
    m.title.text = `${rodash.getString(m.content, "name")} - Page Created at ${date.asTimeStringLoc("h:mm")}:${date.GetSeconds()}`

    m.poster.uri = rodash.getString(m.content, "poster_path").replace("/original/", `/w${m.poster.width}_and_h${m.poster.height}_multi_faces/`)
    m.background.uri = rodash.getString(m.content, "backdrop_path").replace("/original/", `/w1920_and_h1080_multi_faces/`)

    m.aboutUrl = `/details/${m.pageType}/${m.pageId}/about`
    m.relatedUrl = `/details/${m.pageType}/${m.pageId}/related`

    return promises.resolve(true)
end function

function onKeyEvent(key as String, press as Boolean) as Boolean
    if NOT press then return true

    if key = "right" then
        m.lastFocused = m.childOutlet
        return m.childOutlet@.setFocus(true)
    else if key = "left" then
        m.lastFocused = m.buttonGroup
        return m.buttonGroup.setFocus(true)
    end if

    return false
end function

function handleFocus(data as Object) as Boolean
    if data.routerHasFocus then
        m.buttonGroup.focusButton = m.top.route.path.endsWith("related") ? 1 : 0

        if rodash.isEqual(m.lastFocused, m.buttonGroup) then
            return m.buttonGroup.setFocus(true)
        else
            return m.childOutlet@.setFocus(true)
        end if
    end if

    return false
end function


