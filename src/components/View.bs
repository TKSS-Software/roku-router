import "pkg:/source/router.bs"
import "pkg:/source/roku_modules/rodash/rodash.brs"
import "pkg:/source/roku_modules/promises/promises.brs"

sub init()
    m._allowHandleFocus = false
    m.childOutlet = Invalid
end sub

function _beforeViewOpen(params = {} as Object, isChildView = false as Boolean) as Dynamic
    return promises.chain(beforeViewOpen(params), { params: params, isChildView: isChildView }).then(function(response as Dynamic, context as Dynamic) as Dynamic
        params = context.params
        isChildView = context.isChildView

        if NOT isChildView AND params.route.routeConfig.parent <> "" then
            m.childOutlet = rokuRouter.searchForOutlet(m.top)
            if m.childOutlet <> Invalid then
                m.childOutlet.visible = false
                view = rodash.createNode(params.route.routeConfig.component, {
                    id: params.route.id
                    route: params.route
                    router: m.top.router
                })
                m.childOutlet.findNode("viewTarget").appendChild(view)

                return promises.chain(view@._beforeViewOpen(params, true), response).then(function(response as Dynamic, parentResponse as Dynamic) as Dynamic
                    return parentResponse
                end function).toPromise()
            end if
        end if

        return response
    end function).toPromise()
end function

function beforeViewOpen(params = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _onViewOpen(params = {} as Object, isChildView = false as Boolean) as Dynamic
    toResolve = promises.resolve(Invalid)

    return promises.chain(onViewOpen(params), { params: params, isChildView: isChildView }).then(function(response as Dynamic, context as Dynamic) as Dynamic
        m._allowHandleFocus = true
        params = context.params
        isChildView = context.isChildView

        if NOT isChildView AND m.childOutlet <> Invalid then
            ' TODO: BETTER COMMUNICATION BETWEEN PARENT AND CHILD VIEWS
            view = m.childOutlet.findNode("viewTarget").getChild(0)
            print "view", m.childOutlet, m.childOutlet.findNode("viewTarget"), view
            return promises.chain(view@._onViewOpen(params, true), response).then(function(response as Dynamic, parentResponse as Dynamic) as Dynamic
                m.childOutlet.visible = true
                return parentResponse
            end function).toPromise()
        end if

        return response
    end function).toPromise()
end function

function onViewOpen(params = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _beforeViewClose(params = {} as Object) as Dynamic
    m._allowHandleFocus = false
    return beforeViewClose(params)
end function

function beforeViewClose(params = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _onRouteUpdate(event as RouteUpdateEvent) as Dynamic
    print "Route is updating", event.oldRoute, event.newRoute 'bs:disable-line 1001 LINT3012
    return onRouteUpdate(event)
end function

function onRouteUpdate(event = {} as Object) as Dynamic
    return promises.resolve(Invalid)
end function

function _onViewResume(_ as Dynamic, applyFocus = false as Boolean) as Dynamic
    return onViewResume(_)
end function

function _onViewSuspend(_ as Dynamic) as Dynamic
    return onViewSuspend(_)
end function

function onViewResume(_ as Dynamic) as Dynamic
    return promises.resolve(Invalid)
end function

function onViewSuspend(_ as Dynamic) as Dynamic
    return promises.resolve(Invalid)
end function

function _handleFocus(data = {} as Object) as Boolean
    if m._allowHandleFocus then
        handled = handleFocus(data)
        if handled then
            ' print "View focused", data
        else
            ' print "View not focused", data
        end if
        return handled
    end if
    return false
end function

function handleFocus(data = {} as Object) as Boolean
    return false
end function
