import "pkg:/source/router.bs"
import "pkg:/source/roku_modules/rodash/rodash.brs"
import "pkg:/source/roku_modules/promises/promises.brs"

sub init()
    m._allowHandleFocus = false
    m.top.childOutlet = Invalid
end sub

function _beforeViewOpen(params = {} as Object) as Dynamic
    return promises.chain(beforeViewOpen(params), { params: params }).then(function(response as Dynamic, context as Dynamic) as Dynamic
        params = context.params

        if params.route.parentRendered = false AND params.route.routeConfig.parent <> "" then
            m.top.childOutlet = rokuRouter.searchForOutlet(m.top)
            if m.top.childOutlet <> Invalid then
                m.top.childOutlet.visible = false

                params.route.parentRendered = true

                return promises.chain(m.top.childOutlet@._processNavigation(params.route, true), response).then(function(response as Dynamic, parentResponse as Dynamic) as Dynamic
                    return parentResponse
                end function).toPromise()
            end if
        end if

        return response
    end function).toPromise()
end function

function beforeViewOpen(params = {} as Object) as Dynamic
    return promises.resolve(true)
end function

function _onViewOpen(params = {} as Object) as Dynamic
    return promises.chain(onViewOpen(params)).then(function(response as Dynamic) as Dynamic
        m._allowHandleFocus = true
        if m.top.childOutlet <> Invalid then
            m.top.childOutlet.visible = true
        end if
        return response
    end function).toPromise()
end function

function onViewOpen(params = {} as Object) as Dynamic
    return promises.resolve(true)
end function

function _beforeViewClose(params = {} as Object) as Dynamic
    m._allowHandleFocus = false
    return beforeViewClose(params)
end function

function beforeViewClose(params = {} as Object) as Dynamic
    return promises.resolve(true)
end function

function _onRouteUpdate(event as RouteUpdateEvent) as Dynamic
    print "Route is updating", event.oldRoute, event.newRoute 'bs:disable-line 1001 LINT3012
    if NOT event.newRoute.navigationState.fromPopState then
        rokuRouter.getRouter().processingRouteUpdate = true
        hasSameParent = event.oldRoute.routeConfig.parent = event.newRoute.routeConfig.parent
        if hasSameParent AND m.top.childOutlet <> Invalid then
            syncId = CreateObject("roDeviceInfo").GetRandomUUID()
            event.oldRoute.syncId = syncId
            event.newRoute.syncId = syncId
            event.newRoute.parentRendered = event.oldRoute.parentRendered

            return promises.chain(m.top.childOutlet@._processNavigation(event.newRoute, true), event).then(function(response as Dynamic, context as Dynamic) as Dynamic
                return onRouteUpdate(context)
            end function).finally(function(_ = Invalid)
                rokuRouter.getRouter().processingRouteUpdate = false
            end function).toPromise()
        end if

        rokuRouter.getRouter().processingRouteUpdate = false
    end if

    return onRouteUpdate(event)
end function

function onRouteUpdate(event = {} as Object) as Dynamic
    return promises.resolve(true)
end function

function _onViewResume(_ as Dynamic, applyFocus = false as Boolean) as Dynamic
    return onViewResume(_)
end function

function _onViewSuspend(_ as Dynamic) as Dynamic
    return onViewSuspend(_)
end function

function onViewResume(_ as Dynamic) as Dynamic
    return promises.resolve(true)
end function

function onViewSuspend(_ as Dynamic) as Dynamic
    return promises.resolve(true)
end function

function _handleFocus(data = {} as Object) as Boolean
    if m._allowHandleFocus then
        handled = handleFocus(data)
        if handled then
            ' print "View focused", data
        else
            ' print "View not focused", data
        end if
        return handled
    end if
    return false
end function

function handleFocus(data = {} as Object) as Boolean
    return false
end function
